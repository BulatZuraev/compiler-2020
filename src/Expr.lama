-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn   (string, expr)       |
--        Seq    (expr, expr)         |
--        Skip                        |
--        Read   (string)             |
--        Write  (expr)               |
--        if     (expr, expr, expr)   |
--        While  (expr, expr)         |
--        Repeat (expr, expr)         |
--        Var    (string)             |
--        Ref    (string)             |
--        Const  (int)                |
--        Binop  (string, expr, expr) |
--        Ignore (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[st, w], expr) {
  case expr of
    Const (const) -> [c, const]
  | Var (var) -> [c, st(var)]
  | Binop (op, l, r) -> case evalList(c, {l, r}) of
                         [c, {l, r}] -> [c, evalOp(op, l, r)]
                        esac
  | Skip -> [c, Void]
  | Seq (s1, s2) -> case evalList(c, {s1, s2}) of
                      [c, {_, val}] -> [c, val]
                    esac
  | Read (var) -> case w.readWorld of
                    [val, w] -> [[st <- [var, val], w], Void] esac
  | Write (expr) -> case eval(c, expr) of
                      [[st, w], val] -> [[st, writeWorld(val, w)], Void]
                    esac
  | Assn (var, expr) -> case evalList(c, {var, expr}) of
                          [[st, w], {Ref (var), val}] -> [[st <- [var, val], w], val]
                        esac
  | If (expr, s1, s2) -> case eval(c, expr) of
                           [c, val] -> eval(c, if val then s1 else s2 fi)
                         esac
  | While (expr1, s) -> case eval(c, expr1) of
                          [c, val] -> if val then
                            case evalList(c, {s, expr}) of
                              [c, {_, val}] -> [c, val]
                            esac
                          else [c, Void] fi
                        esac
  | Repeat (s, expr) -> case evalList(c, {s, While (Binop ("==", expr, Const (0)), s)}) of
                          [c, {_, val}] -> [c, val]
                        esac
  | Ref (var) -> [c, Ref (var)]
  | Ignore (expr) -> case eval(c, expr) of
                       [c, _] -> [c, Void]
                     esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
