-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
fun eval ([st, w], stmt) {
  case stmt of
    Seq (s1, s2) -> eval (eval ([st, w], s1), s2)
  | Read (var) -> case w.readWorld of
                    [val, w1] -> [st <- [var, val], w1]
                  esac
  | Write (expr) -> [st, writeWorld (evalExpr (st, expr), w)]
  | Assn (var, expr) -> [st <- [var, evalExpr (st, expr)], w]
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
